plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
}

repositories {
  mavenCentral()
}

ext {
  javaFxVersion = '17.0.10'
}

javafx {
  version = javaFxVersion
  modules = ['javafx.controls', 'javafx.media']
}

dependencies {
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'

  implementation "org.openjfx:javafx-base:${javaFxVersion}:win"
  implementation "org.openjfx:javafx-graphics:${javaFxVersion}:win"
  implementation "org.openjfx:javafx-controls:${javaFxVersion}:win"
  implementation "org.openjfx:javafx-media:${javaFxVersion}:win"
}

application {
  mainModule = 'tzuchi.scaningroomsystem'
  mainClass = 'com.tzuchi.scaningroomsystem.ScanningRoomSystem'
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs += [
          '--module-path', classpath.asPath
  ]
}

// Add this to ensure resources are included
processResources {
  from('src/main/resources') {
    include '**/*.mp3'
  }
}

jar {
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  addOptions('--bind-services')
  forceMerge('jackson')

  launcher {
    name = 'ScanningRoomSystem'
    jvmArgs = ['-Xms128m', '-Xmx512m']
  }

  mergedModule {
    requires('java.base')
    requires('java.desktop')
    requires('java.net.http')
  }

  jpackage {
    installerType = 'msi'
    outputDir = 'installer'
    imageName = 'ScanningRoomSystem'
    skipInstaller = false

    installerName = 'ScanningRoomSystem'
    installerOptions = [
            '--win-dir-chooser',
            '--win-menu',
            '--win-shortcut',
            '--win-per-user-install',
            '--verbose'
    ]

    appVersion = '1.0.0'
    vendor = 'TzuChi'

    jvmArgs = ['-Xms128m', '-Xmx512m']
  }
}

tasks.compileJava {
  inputs.property('moduleName', application.mainModule)
  options.compilerArgs = [
          '--module-path', classpath.asPath
  ]
}

task cleanInstaller(type: Delete) {
  delete "${buildDir}/installer"
}

tasks.named('jpackage').configure {
  dependsOn(cleanInstaller)
}